# =========================================
#  Threads-Agent â€“ Dev helper recipes
# =========================================
set dotenv-load

# ---------- K8s Dev Platform ----------
bootstrap:          # spin local k3d
    ./scripts/dev-up.sh

deploy-dev:         # helm install into k3d
    helm upgrade --install threads ./chart \
         -f chart/values-dev.yaml \
         --wait --timeout 120s

k3d-stop-all:
	k3d cluster stop --all

k3d-nuke-all:
	k3d cluster delete --all

test:               # run unit + e2e
    pytest -q

logs:               # follow all pod logs
    kubectl logs -l app=threads -f --tail=100

# ---------- Lint / Type-check / Tests ----------
lint:
	@echo "âš™  ruff (lint+format) â†’ isort â†’ black"
	ruff check --fix .
	ruff format .
	isort . --profile black --filter-files
	black .

check: lint
	@echo "ðŸ”  mypy type-check"
	mypy services scripts
	@echo "ðŸ§ª  pytest suite"
	pytest -q
	@echo "âœ…  all green"

# ---------- CI-green commit âžœ push âžœ PR ----------
# Usage:
#   just ship "feat: awesome (CRA-123)"
#   just ship "wip: spike" NO_PR=true
ship MESSAGE NO_PR="false":
    just check
    pre-commit run --all-files
    git add -A
    git commit -m "{{MESSAGE}}" --no-verify
    @bash -ceu 'br=$(git branch --show-current); echo "â–¶ push â†’ $br"; git push -u origin "$br"'
    @if [ "{{NO_PR}}" != "true" ] && command -v gh >/dev/null; then \
        echo "â–¶ open PR..."; gh pr create --fill --web || true; \
    else echo "â„¹ï¸Ž  skip PR"; fi

# ---------- Utilities ----------
jaeger-ui:          # open Jaeger in browser (mac)
    open http://localhost:16686 || true

default: deploy-dev
